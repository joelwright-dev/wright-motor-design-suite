// Simple lit mesh shader for the WMDS viewport.

struct Uniforms {
    view_proj: mat4x4<f32>,
    light_dir: vec4<f32>,   // world space, normalised, xyz
    color: vec4<f32>,
    camera_pos: vec4<f32>,
};

@group(0) @binding(0)
var<uniform> u: Uniforms;

struct VsIn {
    @location(0) position: vec3<f32>,
    @location(1) normal: vec3<f32>,
};

struct VsOut {
    @builtin(position) clip: vec4<f32>,
    @location(0) world_pos: vec3<f32>,
    @location(1) normal: vec3<f32>,
};

@vertex
fn vs_main(in: VsIn) -> VsOut {
    var out: VsOut;
    out.clip = u.view_proj * vec4<f32>(in.position, 1.0);
    out.world_pos = in.position;
    out.normal = in.normal;
    return out;
}

@fragment
fn fs_main(in: VsOut) -> @location(0) vec4<f32> {
    var n = normalize(in.normal);
    let v = normalize(u.camera_pos.xyz - in.world_pos);
    // Two-sided lighting so inconsistently oriented faces still read correctly.
    if (dot(n, v) < 0.0) {
        n = -n;
    }
    let l = normalize(u.light_dir.xyz);
    let diffuse = max(dot(n, l), 0.0);
    let h = normalize(l + v);
    let spec = pow(max(dot(n, h), 0.0), 32.0) * 0.25;
    let fill = max(dot(n, normalize(vec3<f32>(-0.3, -0.5, 0.4))), 0.0) * 0.25;
    let shade = 0.22 + 0.65 * diffuse + fill;
    let rgb = u.color.rgb * shade + vec3<f32>(spec);
    return vec4<f32>(rgb, 1.0);
}
